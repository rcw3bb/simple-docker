plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

pluginBundle {
    website = 'https://github.com/rcw3bb/simple-docker'
    vcsUrl = 'https://github.com/rcw3bb/simple-docker'
    tags = ['docker', 'simple-docker', 'ronella']
}

configurations {
    plugin
    implementation.extendsFrom(plugin)

    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(shadowJar)
    }

}

gradlePlugin {
    plugins {
        simpleDocker {
            id = 'xyz.ronella.simple-docker'
            displayName = 'Simple Docker Plugin'
            description = 'The plugin that allows you access to docker commands inside gradle as task'
            implementationClass = 'xyz.ronella.gradle.plugin.simple.docker.SimpleDockerPlugin'
        }
    }
}

shadowJar {
    configurations = [project.configurations.plugin]
    archiveClassifier.set("")
    relocate 'xyz.ronella.trivial', "${project.group}.shadow.trivial"
    relocate 'xyz.ronella.command.arrays', "${project.group}.shadow.command.arrays"
    minimize()
}

ext {
    publishingPath = version.toString().contains("SNAPSHOT") ? 'snapshots' : 'internal'
}

publishing {
    publications {

        pluginMaven(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Simple Docker Plugin'
                description = 'The plugin that allows you access to docker commands inside gradle as task'
                url = 'https://github.com/rcw3bb/simple-docker'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/rcw3bb/simple-docker/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'ron'
                        name = 'Ronaldo Webb'
                        email = 'ron@ronella.xyz'
                    }
                }
                scm {
                    url = 'https://github.com/rcw3bb/simple-docker'
                }
            }

            artifacts {
                groupId 'xyz.ronella.gradle.plugin'
                artifactId project.name
                version version
                artifact "./build/libs/${project.name}-${version}.jar"
            }
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    plugin 'xyz.ronella.casual:trivial-chunk:2.17.0'
    plugin 'xyz.ronella.casual:command-arrays:1.0.0'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.1'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.8.0'
}

test {
    useJUnitPlatform()
}

jar {
    enabled = false
    dependsOn shadowJar
}