plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

ext {
    pluginDescription = 'The plugin that allows you access to docker commands inside gradle as task'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    plugin
    implementation.extendsFrom(plugin)

    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(shadowJar)
    }

}

shadowJar {
    configurations = [project.configurations.plugin]
    archiveClassifier.set("")
    relocate 'xyz.ronella.trivial', "${project.group}.shadow.trivial"
    minimize()
}

pluginBundle {
    website = 'https://github.com/rcw3bb/simple-docker'
    vcsUrl = 'https://github.com/rcw3bb/simple-docker'
    description = pluginDescription
    tags = ['docker', 'simple-docker', 'ronella']
}

gradlePlugin {
    plugins {
        simpleDockerPlugin {
            id = 'xyz.ronella.simple-docker'
            displayName = 'Simple Docker Plugin'
            description = pluginDescription
            implementationClass = 'xyz.ronella.gradle.plugin.simple.docker.SimpleDockerPlugin'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    plugin 'xyz.ronella.casual:trivial-chunk:2.17.1'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.1'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.8.0'
}

test {
    useJUnitPlatform()
}

jar {
    enabled = false
    dependsOn shadowJar
}